apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        applicationId "com.owllife.youtubebookmark"
        minSdkVersion 23
        targetSdkVersion 29

        def versionPropsFile = file('version.properties')
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        versionCode versionProps['VERSION_CODE_APP'] as int
        versionName versionProps['VERSION_NAME_APP'].toString()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "YoutubeBookmark-${defaultConfig.versionName}-${variant.buildType.name}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def roomVersion = "2.2.5"
def coroutineVersion = "1.3.7"
def espressoVersion = "3.2.0"
dependencies {

    // Compile time dependencies
    kapt "androidx.room:room-compiler:$roomVersion"
    kapt 'com.android.databinding:compiler:3.1.4'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    // Application dependencies
    // -> 1. com.google
    implementation 'com.google.firebase:firebase-analytics:17.4.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.1'
    implementation 'com.google.firebase:firebase-auth:19.3.1'
    implementation 'com.google.android.gms:play-services-auth:18.0.0'
    implementation 'com.google.firebase:firebase-firestore:21.4.0'

    // -> 2. androidx
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-guava:$roomVersion"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'

    // -> 3. org.jetbrains
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.1.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    // -> 4. etc
    implementation 'com.ogaclejapan.smarttablayout:library:2.0.0@aar'
    implementation 'com.ogaclejapan.smarttablayout:utils-v4:2.0.0@aar'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.16'
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:10.0.5'
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }

    // unit tests dependencies
    testImplementation "junit:junit:4.13"
    testImplementation 'org.amshove.kluent:kluent:1.14'
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutineVersion"
    testImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    testImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    testImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    testImplementation "io.mockk:mockk:1.10.0"
}

apply plugin: 'com.google.gms.google-services'